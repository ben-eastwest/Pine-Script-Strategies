//************************ BUILT FOR S&P FUTURES ON THE DAILY TIME FRAME **********************************//

//@version=5
strategy("SMA and EMA strategy", overlay=true)

//-- Global variables -- //
pos = strategy.position_size
price = strategy.position_avg_price

// -- Input Variables -- //
show = input.bool(true, 'Show Labels ?')

//-- Trailing stop variables -- //
atr = ta.atr(20)
mult = input.float(2.5, 'atr multi')

//-- Strategy variables --//
ma1 = ta.sma(close,3)
ma2 = ta.sma(close, 5)
ma3 = ta.ema(close, 10)

//--strategy logic--//
entry = ma1 < ma1[4] and ma2 < ma2[4]
exit = close > ma3 and close > price 

// -- Strategy entry --//
if entry and pos == 0
    strategy.entry('Long', strategy.long)
    if show == true
        label.new(bar_index, high + atr * 2, style=label.style_triangledown, color=color.green,size=size.auto, tooltip =
         'SMA3 : '+str.tostring(ma1,'#.##')+'\nSMA3[4] :'+str.tostring(ma1[4],'#.##')+'\nSMA5 :'+str.tostring(ma2 ,'#.##')+'\nSMA5[4] :'+str.tostring(ma2[4]))

//-- Trailing stop loss --- code taken from robot traders --- //
tsl = low[1] - (atr*mult)

long_stop_price = 0.0
long_stop_price := if pos > 0
    math.max(tsl, long_stop_price[1])
else
    0

// -- Strategy exit -- //
if pos > 0
    strategy.exit(id='TSL', stop=long_stop_price)
    if exit
        if show == true
            label.new(bar_index, high + atr * 2, style=label.style_circle, color=color.aqua, size=size.auto, tooltip = 'Close : '+str.tostring(close)+'\nEntry Price : '+str.tostring(price)+
             '\nEMA10 : '+str.tostring(ma3,'#.##'))

        strategy.close(id='Long', comment='Profit')

// -- Plotting -- //
plot(series = pos > 0 ? long_stop_price : na, color=color.red, style=plot.style_linebr, linewidth=2)
plot(ma1, color=#4f708a)
plot(ma2, color=#682828)
plot(ma3, color=#50490e)
